/*
 * Copyright and related rights waived via CC0
 *
 * You should have received a copy of the CC0 legalcode along with this
 * work. If not, see <http://creativecommons.org/publicdomain/zero/1.0/>.
 */
plugins {
    id 'groovy-gradle-plugin'
    id 'maven-publish'
}

repositories {
    mavenCentral()
    mavenLocal()
}

//
// CI override for Native Build Tools version (keeps local default from libs.versions.toml)
// Usage: pass -PnativeBuildToolsVersion=<override-version> or export
// ORG_GRADLE_PROJECT_nativeBuildToolsVersion=<override-version>
//
def nbtOverride = providers.gradleProperty("nativeBuildToolsVersion").orNull
if (nbtOverride) {
    logger.lifecycle("Using native-build-tools version override: ${nbtOverride}")
    configurations.configureEach {
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == "org.graalvm.buildtools" &&
                details.requested.name == "native-gradle-plugin") {
                details.useVersion(nbtOverride)
                details.because("CI override via -PnativeBuildToolsVersion")
            }
            // Also cover plugin marker just in case it's resolved in this project
            if (details.requested.group == "org.graalvm.buildtools.native" &&
                details.requested.name == "org.graalvm.buildtools.native.gradle.plugin") {
                details.useVersion(nbtOverride)
                details.because("CI override via -PnativeBuildToolsVersion (plugin marker)")
            }
        }
    }
}

project.group = "org.graalvm"
project.version = "1.0.0-SNAPSHOT"

publishing {
    publications {
        library(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url = uri("${buildDir}/publishing-repository")
        }
    }
}

dependencies {
    implementation(libs.nativeGradlePlugin)
    implementation 'com.networknt:json-schema-validator:1.5.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.18.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.1'
    testImplementation 'org.assertj:assertj-core:3.23.1'
}

test {
    useJUnitPlatform()
}
